set datafile separator ","
set terminal pngcairo size 960,720 font "Helvetica,14"
set tics font "Helvetica,12"
set title "Time to Print by Precision, LANGUAGE language"

# Background
set object 1 rectangle from screen 0,0 to screen 1,1 fillcolor rgb"#f8f8ff" behind

# Border
set style line 10 lc rgb '#808080' lt 1 lw 3
set border 3 back ls 10
set tics nomirror

# Grid
set style line 11 lc rgb '#404040' lt 0 lw 2
set grid back ls 11

# Axis and Labels
set ylabel "Avg Time to Print Number in ns, Log Scale w/Std Dev"
set logscale y 2
set xlabel "Precision of Input Number"
set xrange [0:18]
set xtics 1, 1, 17

set key center center

# Line Colors
set style line 1 lc rgb '#cb1a0e' lt 1 lw 2  # red
set style line 2 lc rgb '#334c87' lt 1 lw 2  # blue
set style line 3 lc rgb '#5e9c36' lt 1 lw 2  # green

plot "INPUT_FILE" using 1:2:3 ls 1 pt 5 ps 1 title 'snprintf %.17g' with yerrorbars,\
     "INPUT_FILE" using 1:2:(sprintf("%.0f",$2)) ls 1 pt 5 ps 1 with labels offset 1.4,0.5 font ",10" notitle,\
     "INPUT_FILE" using 1:2 ls 1 smooth csplines notitle,\
\
     "INPUT_FILE" using ($1+.2):6:7 ls 2 pt 7 ps 1 title 'Ryu shortest' with yerrorbars,\
     "INPUT_FILE" using ($1+.2):6 ls 2 smooth csplines notitle,\
\
     "INPUT_FILE" using ($1+.2):10:11 ls 3 pt 7 ps 1 title 'Doubleback dfmt' with yerrorbars,\
     "INPUT_FILE" using ($1+.2):10:(sprintf("%.0f",$10)) ls 3 pt 5 ps 1 with labels offset 1.4,0.5 font ",10" notitle,\
     "INPUT_FILE" using ($1+.2):10 ls 3 smooth csplines notitle

